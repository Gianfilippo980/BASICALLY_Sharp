 10:"A" CLEAR : REM **GRAFICO**
 20:INPUT "X1=";X1,"X2=";X2:N=31
 30:DIM X(N): DIM Y(N)
 40:GOSUB "MAX"
 50:SV=(A-B)/N
 60:CLS
 70:USING "###.##"
 80:CURSOR 5,0: PAUSE "X1=";X1;"X2=";X2
 90:USING "#####.##"
100:CURSOR 5,1: PAUSE "MAX";XA;A
110:CURSOR 5,2: PAUSE "MIN";XB;B
120:WAIT 0
130:LINE (0,15)-(30,15): LINE (15,0)-(1
    5,31)
140:IN=0
150:FOR I=0 TO N
160:USING "###."
170:J= MDF ((A-Y(I))/SV)
180:USING
190:PSET (I,J)
200:IF I>0 LET IN=IN+(Y(I)+Y(I-1))*(X2-
    X1)/(2*30)
210:NEXT I
220:WAIT
230:BEEP 3
240:CURSOR 5,3: PRINT "INT";IN
250:CLS
260:END
270:"F": REM FUNZIONE
280:Y= SIN (X)
290:RETURN
300:"MAX":X=X1
310:GOSUB "F"
320:X(0)=X:Y(0)=Y
330:A=Y:B=Y:XA=X:XB=X
340:P=(X2-X1)/N
350:FOR I=0 TO N
360:X=X1+I*P
370:GOSUB "F"
380:X(I)=X:Y(I)=Y
390:IF Y>A LET A=Y: LET XA=X
400:IF Y<B LET B=Y: LET XB=X
410:NEXT I
420:RETURN
430:"B" INPUT "X1=";X1,"X2=";X2,"Ultima
     differenza=";E: REM PARABOLE
440:I=6
450:INPUT "Minimo punti=(6)";I
460:ERASE Y:V= RND 10
470:DIM Y(2)
480:C=0
490:P=(X2-X1)/(I-1)
500:FOR J=2 TO I STEP 2
510:X=X1+(J-2)*P
520:GOSUB "F"
530:Y(0)=Y
540:X=X1+(J-1)*P
550:GOSUB "F"
560:Y(1)=Y
570:X=X1+J*P
580:GOSUB "F"
590:Y(2)=Y
600:C=C+P/3*(Y(0)+4*Y(1)+Y(2))
610:NEXT J
620:IF ABS (C-V)<=E GOTO 670
630:PAUSE "Corrente";I;C
640:I=2* INT (3/4*I)
650:V=C
660:GOTO 480
670:BEEP 3
680:PRINT "Stima finale";C
690:END
700:"C": INPUT "X1=";X1,"X2=";X2,"Ultim
    a differenza=";E: REM TRAPEZI
710:I=6
720:INPUT "Passi minimi=(6)";I
730:O= RND 10
740:X=X1
750:GOSUB "F"
760:V=Y
770:IN=0
780:P=(X2-X1)/I
790:FOR J=1 TO I
800:X=X1+J*P
810:GOSUB "F"
820:IN=IN+(Y+V)*P/2
830:V=Y
840:NEXT J
850:IF ABS (IN-O)<=E GOTO 900
860:PAUSE "Stima";I;" ";IN
870:O=IN
880:I= INT (3/2*I)
890:GOTO 770
900:BEEP 3
910:PRINT "Risultato",IN
920:END
930:FOR I=1 TO 3
940:FOR J=5 TO 25 STEP 5
950:BEEP I,J,150
960:NEXT J
970:NEXT I
980:END
990:"D": CLEAR : REM MONTECARLO
 1000:N=10
 1010:INPUT "X1=";X1,"X2=";X2,"Ultima d
      ifferenza=";E,"Minimo punti=(10)"
      ;N
 1020:DIM X(N): DIM Y(N)
 1030:GOSUB "MAX"
 1040:A=A+(A-B)/10
 1050:B=B-(A-B)/10
 1060:K=(X2-X1)*(A-B)
 1070:V=K:S=0:T=0
 1080:SX=X2-X1
 1090:SY=A-B
 1100:FOR J=1 TO N
 1110:X=SX* RND 0.2+X1
 1120:GOSUB "F"
 1130:D=SY* RND 0.2+B
 1140:IF 0<D AND D<=Y LET S=S+1
 1150:IF Y<=D AND D<0 LET S=S-1
 1160:T=T+1
 1170:NEXT J
 1180:C=K*S/T
 1190:IF B>0 LET C=C+(X2-X1)*B
 1200:IF A<0 LET C=C-(X2-X1)*A
 1210:IF ABS (C-V)<=E GOTO 1260
 1220:PAUSE "Stima";T;" ";C
 1230:V=C
 1240:N=T/2
 1250:GOTO 1100
 1260:BEEP 3
 1270:PRINT "Risultato=";C
 1280:END
 1290:"G": WAIT 0: CLEAR : LPRINT : 
      CONSOLE 39: DEGREE
 1300:LPRINT : LTEXT : LPRINT : GRAPH :
      GLCURSOR (240,0): SORGN
 1310:CLS : USING : PRINT "** ROUND GRA
      PHIC **"
 1320:INPUT "COLOR(0-6)=";M: COLOR M,7
 1330:INPUT "r=";C,"L=";L
 1340:R=200:H=10: CLS
 1350:CURSOR 5,1: PRINT "** PRINTING **
      "
 1360:P=10000
 1370:D=0: GOSUB "CAL":U=O:V=Q:E=O:F=Q:
      GLCURSOR (U,V)
 1380:FOR I=H TO P STEP H
 1390:D=I: GOSUB "CAL":W=O:X=Q
 1400:LLINE -(W,X)
 1410:U=W:V=X: IF F=V IF E=U THEN 1430
 1420:NEXT I
 1430:CLS : LTEXT : LPRINT : LF 10: END
 1440:"CAL":G=R/C*D:S=(R-C)* COS D:T=(R
      -C)* SIN D:K=L* COS (90-G):N=L* 
      SIN (90-G)
 1450:A= SIN (D-90):B= COS (D-90)
 1460:O=K*B-A*N+S:Q=K*A+B*N+T
 1470:RETURN
 1480:"L": CLEAR : REM STAMPA INTESTAZI
      ONE
 1490:DIM DE$(0)*38: DIM SC$(0)*10
 1500:INPUT "Nome?",NO$,"Scorciatoia?",
      SC$,"Descrizione?",DE$(0)
 1510:FOR I=1 TO 10- LEN SC$
 1520:SC$=" "+SC$
 1530:NEXT I
 1540:COLOR 3: CSIZE 4
 1550:LPRINT NO$: LF -1
 1560:COLOR 1: CSIZE 8
 1570:LPRINT SC$: LF -1
 1580:CSIZE 2: COLOR 2: LF 1
 1590:LPRINT DE$(0)
 1600:LF 1: COLOR 0
 1610:END
 1620:"H": REM **STAMPA GRAFICI**
 1630:CLEAR :N=20: LTEXT : LPRINT : 
      COLOR 0
 1640:INPUT "X1=";X1,"X2=";X2,"SEGMENTI
      =(20)";N
 1650:DIM Y(N): DIM X(N)
 1660:GOSUB "MAX"
 1670:IF A>0 LET A= INT (A*1.1)+1
 1680:IF A=0 LET A=1
 1690:IF A<0 LET A= INT (A*0.9)+2
 1700:IF B<0 LET B= INT (B*1.1)
 1710:IF B=0 LET B=-1
 1720:IF B>0 LET B= INT (B*0.9)
 1730:GOSUB "CART"
 1740:C=480/(X2-X1):D=480/(A-B)
 1750:L=-B*D-240
 1760:IF L>240 LET L=240
 1770:IF L<-240 LET L=-240
 1780:LLINE (-240,L)-(240,L),0,1
 1790:FOR I=1 TO N
 1800:E=(X(I-1)-X1)*C-240:F=(Y(I-1)-B)*
      D-240
 1810:G=(X(I)-X1)*C-240:H=(Y(I)-B)*D-24
      0
 1820:LLINE (E,F)-(G,H),0,2
 1830:IF I<N LLINE (G,H)-(G,L),0,1
 1840:K=K+(X(I)-X(I-1))*(Y(I)+Y(I-1))/2
 1850:NEXT I
 1860:GLCURSOR (-240,-240)
 1870:COLOR 0,7: LTEXT
 1880:LF 3
 1890:LPRINT "Integrale=";K
 1900:END
 1910:"CART": REM PIANO CARTESIANO
 1920:LPRINT "                    ";A
 1930:GRAPH : COLOR 0
 1940:GLCURSOR (240,-240)
 1950:SORGN
 1960:LLINE (0,-240)-(0,240)
 1970:LLINE (-240,0)-(240,0)
 1980:LLINE (-240,-240)-(240,240),0,0,B
 1990:COLOR 5,7
 2000:FOR I=1 TO 9
 2010:IF I=5 NEXT I
 2020:LLINE (48*(I-5),240)-(48*(I-5),-2
      40)
 2030:LLINE (-240,48*(I-5))-(240,48*(I-
      5))
 2040:NEXT I
 2050:COLOR 0,7
 2060:GLCURSOR (0,-260)
 2070:LPRINT "P";B
 2080:GLCURSOR (-240,-280)
 2090:LPRINT "PX1=";X1
 2100:GLCURSOR (100,-280)
 2110:LPRINT "PX2=";X2
 2120:RETURN
 2130:"DATI": REM INSERIMENTO DATI
 2140:INPUT "Numero di dati=";N
 2150:INPUT "Errore X (Fisso/Percentual
      e/Assoluto)?";G$
 2160:IF G$="P" OR G$="F" INPUT "Valore
       errore X=";H
 2170:INPUT "Errore Y (Fisso/Percentual
      e/Assoluto)?";J$
 2180:IF J$="P" OR J$="F" INPUT "Valore
       errore Y=";L
 2190:DIM X(N-1,1): DIM Y(N-1,1)
 2200:FOR I=0 TO N-1
 2210:PAUSE "X"+ STR$ I+"="
 2220:INPUT X(I,0)
 2230:IF G$="A" PAUSE "Errore X"+ STR$ 
      I+"="
 2240:IF G$="A" INPUT X(I,1)
 2250:PAUSE "Y"+ STR$ I+"="
 2260:INPUT Y(I,0)
 2270:IF J$="A" PAUSE "Errore Y"+ STR$ 
      I+"="
 2280:IF J$="A" INPUT Y(I,1)
 2290:IF G$="F" LET X(I,1)=H
 2300:IF G$="P" LET X(I,1)=H*X(I,0)/100
 2310:IF J$="F" LET Y(I,1)=L
 2320:IF J$="P" LET Y(I,1)=L*Y(I,0)/100
 2330:NEXT I
 2340:RETURN
 2350:"J": CLEAR : REM **REGRESSIONE SE
      MPLICE**
 2360:GOSUB "DATI"
 2370:FOR I=0 TO N-1
 2380:E=E+X(I,0):F=F+Y(I,0)
 2390:NEXT I
 2400:E=E/N:F=F/N
 2410:G=0:H=0
 2420:FOR I=0 TO N-1
 2430:G=G+(X(I,0)-E)*(Y(I,0)-F)
 2440:H=H+(X(I,0)-E)^2
 2450:NEXT I
 2460:G=G/H
 2470:H=F-G*E
 2475:BEEP 3
 2480:PAUSE "Pendenza=";G
 2485:PAUSE "Intercetta=";H
 2490:INPUT "Stampare?(S/N)";K$
 2500:IF K$="S" LTEXT : COLOR 0,7: 
      LPRINT "Regressione lineare sempl
      ice"
 2510:IF K$="S" GOSUB "REG"
 2515:IF K$="N" GOSUB "DISP"
 2520:END
 2530:"REG": REM STAMPA REGRESSIONE
 2540:COLOR 0,7: LTEXT
 2550:GOSUB "EST"
 2560:GOSUB "CART"
 2570:C=480/(X2-X1):D=480/(A-B)
 2580:FOR I=0 TO N-1
 2590:L=(X(I,0)-X1)*C-240
 2600:M=(Y(I,0)-B)*D-240
 2610:LLINE (L-2,M-2)-(L+2,M+2),0,2,B
 2620:LLINE (L-X(I,1)*C,M-Y(I,1)*D)-(L+
      X(I,1)*C,M+Y(I,1)*D),0,4,B
 2630:NEXT I
 2640:O=X1:P=H+X1*G
 2650:Q=X2:R=H+X2*G
 2660:IF P>A LET P=A: LET O=(P-H)/G
 2670:IF P<B LET P=B: LET O=(P-H)/G
 2680:IF R>A LET R=A: LET Q=(R-H)/G
 2690:IF R<B LET R=B: LET Q=(R-H)/G
 2700:O=(O-X1)*C-240:P=(P-B)*D-240
 2710:Q=(Q-X1)*C-240:R=(R-B)*D-240
 2720:LLINE (O,P)-(Q,R),0,1
 2730:GLCURSOR (0,-280)
 2740:LTEXT : LPRINT : COLOR 0,7
 2750:LPRINT "Pendenza=";G
 2760:LPRINT "Intercetta=";H
 2770:INPUT "Stampare tabella? (S/N)";S
      $
 2780:IF S$="S" COLOR 2,7: LPRINT "    
            X","          Y","         
      DY": COLOR 0,7: LPRINT
 2790:FOR I=0 TO N-1
 2800:IF S$="S" LPRINT X(I,0),Y(I,0),Y(
      I,1)
 2810:NEXT I
 2820:RETURN
 2830:"EST": REM ESTREMI
 2840:A=Y(0,0):B=Y(0,0):X1=X(0,0):X2=X(
      0,0)
 2850:FOR I=0 TO N-1
 2860:IF X(I,0)<X1 LET X1=X(I,0)
 2870:IF X(I,0)>X2 LET X2=X(I,0)
 2880:IF Y(I,0)>A LET A=Y(I,0)
 2890:IF Y(I,0)<B LET B=Y(I,0)
 2900:NEXT I
 2910:IF A>0 LET A= INT (A*1.1)+1
 2920:IF A=0 LET A=1
 2930:IF A<0 LET A= INT (A*0.9)+2
 2940:IF B<0 LET B= INT (B*1.1)
 2950:IF B=0 LET B=-1
 2960:IF B>0 LET B= INT (B*0.9)
 2970:IF X2>0 LET X2= INT (X2*1.1)+1
 2980:IF X2=0 LET X2=1
 2990:IF X2<0 LET X2= INT (X2*0.9)+2
 3000:IF X1<0 LET X1= INT (X1*1.1)
 3010:IF X1=0 LET X1=-1
 3020:IF X1>0 LET X1= INT (X1*0.9)
 3030:RETURN
 3040:"K": REM **REGRESSIONE LINEARE PE
      SATA**
 3050:CLEAR
 3060:GOSUB "DATI"
 3070:CLEAR A,B,C,D
 3080:FOR I=0 TO N-1
 3090:A=A+1/Y(I,1)
 3100:B=B+X(I,0)/Y(I,1)
 3110:C=C+X(I,0)^2/Y(I,1)
 3120:D=D+X(I,0)*Y(I,0)/Y(I,1)
 3130:E=E+Y(I,0)/Y(I,1)
 3140:NEXT I
 3150:G=(A*D-B*E)/(A*C-B^2)
 3160:H=(C*E-B*D)/(A*C-B^2)
 3165:BEEP 3
 3170:PAUSE "Pendenza=";G
 3175:PAUSE "Intercetta=";H
 3180:INPUT "Stampare (S/N)?";F$
 3190:IF F$="S" LTEXT : COLOR 0,7: 
      LPRINT "Regressione lineare ponde
      rata"
 3200:IF F$="S" GOSUB "REG"
 3205:IF F$="N" GOSUB "DISP"
 3210:END
 3220:"DISP": REM VIDEO REGRESSIONE
 3230:CLS
 3240:GOSUB "EST"
 3250:K=31/(X2-X1)
 3260:L=31/(A-B)
 3270:CLS
 3280:WAIT 0
 3290:LINE (0,15)-(30,15): LINE (15,0)-
      (15,31)
 3300:FOR I=0 TO N-1
 3310:O=(X(I,0)-X1)*K
 3320:P=(A-Y(I,0))*L
 3330:PSET (O,P),X
 3340:NEXT I
 3350:Q=(A-(H+G*X1))*L
 3360:R=(A-(H+G*X2))*L
 3365:WAIT
 3370:LINE (0,Q)-(31,R),X
 3380:CURSOR 5,0: PAUSE "X1=";X1;"X2=";
      X2
 3390:CURSOR 5,1: PAUSE "MAX=";A;"MIN="
      ;B
 3400:CURSOR 5,2: PAUSE "Pend=";G
 3410:CURSOR 5,3: PRINT "Inter=";H
 3420:CLS
 3430:RETURN
 3440:"M": REM **Campo Minato**
 3450:CLEAR :N=15: REM RANDOM
 3460:INPUT "Numero di mine (15)=";N
 3470:DIM CM$(3,19)*1
 3480:FOR I=1 TO N
 3490:A= RND 4
 3500:A=A-1
 3510:B= RND 20
 3520:B=B-1
 3530:IF CM$(A,B)="*" THEN LET I=I-1: 
      NEXT I
 3540:CM$(A,B)="*"
 3550:NEXT I
 3560:FOR J=0 TO 3
 3570:FOR I=0 TO 19
 3575:IF CM$(J,I)="*" THEN GOTO 3650
 3580:M=0
 3590:IF I>0 THEN IF CM$(J,I-1)="*" 
      THEN LET M=M+1
 3600:IF I<19 THEN IF CM$(J,I+1)="*" 
      THEN LET M=M+1
 3610:IF J>0 THEN IF CM$(J-1,I)="*" 
      THEN LET M=M+1
 3620:IF J<3 THEN IF CM$(J+1,I)="*" 
      THEN LET M=M+1
 3630:IF M>0 THEN LET CM$(J,I)= STR$ (M
      )
 3640:IF M=0 THEN LET CM$(J,I)=" "
 3650:NEXT I
 3660:NEXT J
 3700:FOR J=0 TO 3
 3710:FOR I=0 TO 19
 3720:CURSOR I,J
 3730:PAUSE "#"
 3740:NEXT I
 3750:NEXT J
 3760:CURSOR 20,0
 3770:PAUSE "Mine"
 3780:CURSOR 20,1
 3790:PAUSE N-T
 3810:GOSUB "MOSSA"
 3815:CURSOR X,Y
 3820:IF CM$(X,Y)="*" AND M$="M" THEN 
      PAUSE "^":T+=1
 3830:IF CM$(X,Y)="*"
 4000:INPUT X
 4010:I=-35.55
 4020:C=2.961
 4030:PRINT (X-I)/C
 4040:GOTO 4000
